apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        flavorDimensions "versionCode"
    }

    buildToolsVersion = '28.0.3'
    useLibrary 'org.apache.http.legacy'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //签名
    Properties properties = new Properties()
    properties.load(project.rootProject.file('perry.properties').newDataInputStream())
    signingConfigs {
        release_key {
            storeFile file(properties.getProperty("keystroe_storeFile"))
            storePassword properties.getProperty("keystroe_storePassword")
            keyAlias properties.getProperty("keystroe_keyAlias")
            keyPassword properties.getProperty("keystroe_keyPassword")
        }
    }

    //打包类型
    buildTypes {
        debug {
            minifyEnabled false     // 混淆
            zipAlignEnabled false   // Zipalign优化
        }
        release {
            minifyEnabled false      // 混淆
            zipAlignEnabled true    // Zipalign优化
            shrinkResources false    // 移除无用的resource文件
            // 前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_key
        }
    }

    //配置自定义打包名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date()
            def formattedDate = date.format('yyyyMMdd')
            def fileName = "Lizhi_v${variant.versionName}_${variant.productFlavors[0].name}_${formattedDate}_${variant.buildType.name}.apk"
            outputFileName = fileName
        }
    }

    //配置多渠道打包
    productFlavors {

        beta {
            versionCode 1
            versionName "1.0_beta"
            applicationId "ren.perry.lizhi.beta"
            resValue "string", "app_name", "我爱南京Beta"
            buildConfigField 'String', 'SERVER_URL', '"http://ngrok.perry.ren:27020"'
            buildConfigField 'String', 'SERVER_MODULE', '"/"'
        }

        perry {
            versionCode 1
            versionName "1.0"
            applicationId "ren.perry.lizhi"
            resValue "string", "app_name", "我爱南京"
            buildConfigField 'String', 'SERVER_URL', '"https://lz.perry.ren"'
            buildConfigField 'String', 'SERVER_MODULE', '"/api/lz"'
        }
    }
}

//GreenDao
greendao {
    schemaVersion 1                         //数据库版本号
    daoPackage 'ren.perry.lizhi.dao'      //设置DaoMaster、DaoSession、Dao包名
    targetGenDir 'src/main/java'            //设置DaoMaster、DaoSession、Dao目录
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':mvplibrary')

    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'    //动态权限请求
    implementation 'com.allenliu.versionchecklib:library:2.1.9'             //版本更新库
    implementation 'com.orhanobut:logger:2.2.0'                             //日志打印

    implementation 'com.youth.banner:banner:1.4.10'                         //轮播图控件
    implementation 'com.just.agentweb:agentweb:4.0.3-alpha@aar'             //WebView
    implementation 'org.greenrobot:greendao:3.2.2'                          //数据库操作
    implementation 'net.zetetic:android-database-sqlcipher:4.1.3@aar'       //数据库加密
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'            //调试数据库
    debugImplementation 'com.amitshekhar.android:debug-db-encrypt:1.0.6'    //调试数据库

}
